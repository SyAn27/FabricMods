plugins {
    id 'java'
    id 'maven-publish'
    //id 'fabric-loom' version '0.2.7-SNAPSHOT' apply false
    id 'fabric-loom' version '0.4.3' apply false
    id 'com.matthewprenger.cursegradle' version '1.4.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'fabric-loom'
    apply plugin: 'com.matthewprenger.cursegradle'

    group = maven_group
    archivesBaseName = mod_id
    version = "${mod_version}+${minecraft_version}"

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    minecraft {
        refmapName = "${mod_id}-refmap.json"
        runDir = "${project.projectDir.getName()}/run/"
    }

    ext {
        dep_chainmail_version = findProject(":chainmail").mod_version
        dep_container_lib_version = findProject(":ninjaphenix-container-library").mod_version
        withoutUnwanted = {
            exclude(module: "minecraft")
            exclude(module: "fabric-loader")
            exclude(module: "fabric-api")
            exclude(module: "modmenu")
        }
    }

    repositories {
        mavenLocal()
        maven { url "http://server.bbkr.space:8081/artifactory/libs-release/" }
    }

    dependencies {
        minecraft("com.mojang:minecraft:${minecraft_version}")
        mappings("net.fabricmc:yarn:${minecraft_version}+build.${yarn_version}:v2")
        modImplementation("net.fabricmc:fabric-loader:${loader_version}")
        modImplementation("net.fabricmc.fabric-api:fabric-api:${fabric_api_version}")

        modImplementation "me.shedaniel:RoughlyEnoughItems:${rei_version}", withoutUnwanted

        modImplementation "io.github.prospector:modmenu:${modmenu_version}", withoutUnwanted

        modImplementation "io.github.cottonmc:Jankson-Fabric:${jankson_version}", withoutUnwanted

        compileOnly("org.jetbrains:annotations:19.0.0")
    }

    processResources {
        inputs.properties(
                "mod_version": mod_version,
                "minecraft_version": minecraft_version,
                "dep_minecraft_version": dep_minecraft_version,
                "dep_loader_version": dep_loader_version,
                "dep_fabric_version": dep_fabric_version,
                "dep_container_lib_version": dep_container_lib_version,
                "dep_chainmail_version": dep_chainmail_version
        )
        from(sourceSets.main.resources.srcDirs) {
            include("fabric.mod.json")
            expand(
                    "mod_version": mod_version,
                    "minecraft_version": minecraft_version,
                    "dep_minecraft_version": dep_minecraft_version,
                    "dep_loader_version": dep_loader_version,
                    "dep_fabric_version": dep_fabric_version,
                    "dep_container_lib_version": dep_container_lib_version,
                    "dep_chainmail_version": dep_chainmail_version
            )
        }
        from(sourceSets.main.resources.srcDirs) {
            exclude("fabric.mod.json")
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    jar { from("LICENSE") }

    runClient {
        args = ["--username", "Dev"]
    }

    tasks.withType(JavaCompile) { options.setEncoding("UTF-8") }

    task tagRelease(type:Exec) {
        def tagName = "${mod_id}-${mod_version}+${minecraft_version}"
        commandLine "git", "tag", "-a", tagName, "-m", tagName
        doLast {
            println "Creating tag: " + tagName
        }
    }

    task tagReleaseAndPush(type:Exec) {
        dependsOn tagRelease
        def tagName = "${mod_id}-${mod_version}+${minecraft_version}"
        commandLine "git", "push", "origin", tagName
        doLast {
            println "Pushed tag " + tagName + " to origin"
        }
    }

    if (project.hasProperty("api_key") && project.hasProperty("curseforge_project_id")) {
        curseforge {
            apiKey = project.api_key
            //noinspection GroovyAssignabilityCheck
            project {
                id = "${curseforge_project_id}"
                changelogType = "markdown"
                changelog = file("${project.projectDir}/changelog.md")
                releaseType = "${project.curseforge_release_type}"
                addGameVersion "${project.curseforge_game_version}"
                addGameVersion "Fabric"
                addGameVersion "Java 8" // lets be honest, we're going to be stuck on java 8 for the next 5 years.
                relations {
                    if(project.hasProperty("curseforge_required_dependencies")) {
                        for(String dep : project.curseforge_required_dependencies.split(":")) {
                            requiredDependency dep
                        }
                    }
                    if(project.hasProperty("curseforge_embedded_dependencies")) {
                        for(String dep : project.curseforge_embedded_dependencies.split(":")) {
                            embeddedLibrary dep
                        }
                    }
                    if(project.hasProperty("curseforge_optional_dependencies")) {
                        for(String dep : project.curseforge_optional_dependencies.split(":")) {
                            optionalDependency dep
                        }
                    }
                }
                mainArtifact(file("${project.buildDir}/libs/${mod_id}-${mod_version}+${minecraft_version}.jar")) {
                    displayName = "${mod_name} ${mod_version}+${minecraft_version}"
                }
                addArtifact(file("${project.buildDir}/libs/${mod_id}-${mod_version}+${minecraft_version}-sources.jar"))
                afterEvaluate {
                    uploadTask.dependsOn("remapSourcesJar")
                }
            }
            options {
                forgeGradleIntegration = false
                debug = curseforge_debug == "true"
            }
        }

        tagRelease {
            dependsOn "curseforge"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = mod_id
                artifact(remapJar) {
                    builtBy remapJar
                }
                artifact(sourcesJar) {
                    classifier = "sources"
                    builtBy remapSourcesJar
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }
}